plugins {
    id 'net.minecraftforge.gradle.forge' version '6f5327738d'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'java'
}

version = mod_ver
group = mod_group
archivesBaseName = mod_name

sourceCompatibility = targetCompatibility = 1.8
compileJava.options.encoding = 'UTF-8'

minecraft {
    version = '1.8.9-11.15.1.2318-1.8.9'
    runDir = 'run'
    replace('@VER@', mod_ver)
    mappings = 'stable_22'
}

configurations {
    shade
    implementation.extendsFrom(shade)
}

repositories {
    maven {
        name = 'JitPack'
        url = 'https://jitpack.io'
    }
    maven {
        name = 'Sk1er'
        url = 'https://repo.sk1er.club/repository/maven-public'
    }
}

dependencies {
    shade('com.github.jncrmx:discord-game-sdk4j:0.5.3')
    shade('gg.essential:Vigilance:123-10809-SNAPSHOT')
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest.attributes(
            "ModSide": "CLIENT"
    )
    exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
}

shadowJar {
    configurations = [project.configurations.shade]
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    archiveClassifier.set('')
}

processResources {
    inputs.property 'version', project.version
    inputs.property 'mcversion', project.minecraft.version
    inputs.property 'name', mod_name
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand (
                'version':project.version,
                'mcversion':project.minecraft.version,
                'name':mod_name
        )
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task moveResources {
    doLast {
        ant.move file: "${buildDir}/resources/main",
                todir: "${buildDir}/classes/java"
    }
}

tasks.moveResources.dependsOn processResources
tasks.classes.dependsOn moveResources
tasks.reobfJar.dependsOn(shadowJar)