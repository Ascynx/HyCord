buildscript {
    repositories {
        maven {
            name = "Forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "JitPack"
            url = "https://jitpack.io"
        }
    }
    dependencies {
        classpath "com.github.asbyth:ForgeGradle:86b2392"
        classpath 'com.github.johnrengelman:shadow:4.0.4'
    }
}

apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: 'com.github.johnrengelman.shadow'

version = project.mod_ver
group = project.mod_group
archivesBaseName = project.mod_name

sourceCompatibility = targetCompatibility = 1.8
compileJava.options.encoding = 'UTF-8'

minecraft {
    version = project.forge_ver
    runDir = "run"
    replace('@VER@', project.version)
    mappings = project.minecraft_mappings
}

repositories {
    flatDir dirs: 'libs'
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    provided files('libs/modcore.jar')
    shadow 'com.github.jncrmx:discord-game-sdk4j:0.5.3'
    implementation 'com.github.JnCrMx:discord-game-sdk4j:v0.5.3'
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

shadowJar {
    project.configurations.shadow.setTransitive(true);
    configurations = [project.configurations.shadow]
    manifest.attributes(
            "FMLCorePlugin": "io.github.dediamondpro.hycord.tweaker.FMLLoadingPlugin",
            "ModSide": "CLIENT",
            "FMLCorePluginContainsFMLMod": true
    )

    classifier '' // Replace the default JAR
}

reobf {
    shadowJar {} // Reobfuscate the shadowed JAR
}

jar.finalizedBy('reobfJar')
