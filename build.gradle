plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'net.minecraftforge.gradle.forge' version 'b13624b'
}

version = project.mod_ver
group = project.mod_group
archivesBaseName = project.mod_name

sourceCompatibility = targetCompatibility = 1.8
compileJava.options.encoding = 'UTF-8'

minecraft {
    version = '1.8.9-11.15.1.2318-1.8.9'
    runDir = "run"
    replace('@VER@', project.version)
    mappings = 'stable_22'
    makeObfSourceJar = false
}

repositories {
    maven {
        name = 'JitPack'
        url = 'https://jitpack.io' }
}

configurations {
    include
    implementation.extendsFrom(include)
}

dependencies {
    provided files('libs/modcore.jar')

    include 'com.github.jncrmx:discord-game-sdk4j:v0.5.3'
}

jar {
    manifest {
        attributes (
                'FMLCorePlugin': 'io.github.dediamondpro.hycord.tweaker.FMLLoadingPlugin',
                'ModSide': 'CLIENT',
                'FMLCorePluginContainsFMLMod': 'Yes, yes it does'
        )
    }
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

// Forge does not import resources properly.
task moveResources {
    doLast {
        ant.move file: "${buildDir}/resources/main",
                todir: "${buildDir}/classes/java"
    }
}

tasks.moveResources.dependsOn processResources
tasks.classes.dependsOn moveResources

shadowJar {
    configurations = [project.configurations.include]
    duplicatesStrategy DuplicatesStrategy.EXCLUDE

    archiveClassifier.set('')
}

jar.dependsOn shadowJar
